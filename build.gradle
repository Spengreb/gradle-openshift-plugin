plugins {
  id "com.iadams.gradle-plugin-plugin" version "0.2"
}

group 'com.iadams.gradle'
version '0.1-RC1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile 'io.fabric8:openshift-client:2.2.13'

  testCompile 'commons-io:commons-io:2.4'
  testCompile 'io.fabric8:kubernetes-client:2.2.13:tests'
  testCompile 'io.fabric8:openshift-client:2.2.13:tests'
  testCompile 'io.fabric8:openshift-server-mock:2.2.13'

  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
    exclude module: 'groovy-all'
  }
}

tasks.withType(Test) {
  testLogging {
    events "failed", "passed", "skipped"
    exceptionFormat "full"
  }
}

task integTestTarBall(type: Tar) {
  from files('src/integTest/resources/docker-build/Dockerfile', 'src/integTest/resources/docker-build/server.js')
  destinationDir = file('src/integTest/resources/docker-build')
  archiveName = 'build.tar.gz'
  compression = Compression.GZIP
}

processIntegTestResources.dependsOn integTestTarBall

pluginBundle {
  website = 'https://github.com/iwarapter/gradle-openshift-plugin'
  vcsUrl = 'https://github.com/iwarapter/gradle-openshift-plugin'
  description = 'Plugin for integrating with OpenShift.'
  tags = ['openshift']

  plugins {
    openshiftPlugin {
      id = 'com.iadams.openshift-base'
      displayName = 'Gradle OpenShift Plugin'
    }
  }
}